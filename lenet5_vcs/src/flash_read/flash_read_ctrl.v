/*
 * @Author: Wang Yuanhao
 * @Date: 2022-10-13 10:04:41
 * @LastEditTime: 2022-10-13 16:15:26
 * @LastEditors: Wang Yuanhao
 * @Description: QSPI flash read controller based on EmbedFire education version.
 * @FilePath: \flash_read\flash_read_ctrl.v
 * Change history:
 */

module  flash_read_ctrl(
    input   wire            sys_clk     ,
    input   wire            sys_rst_n   ,
    input   wire            start       ,
    input   wire    [23:0]  start_addr  ,   //è¯»å–çš„åˆå§‹åœ°å?
    input   wire    [23:0]  byte_num    ,   //ä¸?æ¬¡éœ€è¦è¯»å‡ºçš„å­—èŠ‚æ•?
    input   wire            miso        ,   //è¯»å‡ºflashæ•°æ®

    output  reg             sck         ,   //ä¸²è¡Œæ—¶é’Ÿ
    output  reg             cs_n        ,   //ç‰‡é?‰ä¿¡å?
    output  reg             mosi        ,   //ä¸»è¾“å‡ºä»è¾“å…¥æ•°æ®
    output  reg             po_flag     ,   //è¾“å‡ºæ•°æ®
    output  reg     [7:0]   po_data         //è¾“å‡ºæ•°æ®
);

//********************************************************************//
//****************** Parameter and Internal Signal *******************//
//********************************************************************//

//parameter define
parameter   IDLE    =   2'b01  ,   //åˆå§‹çŠ¶æ??
            READ    =   2'b10  ;   //æ•°æ®è¯»çŠ¶æ€?

wire [7:0]  READ_INST;
assign  READ_INST  =   8'b0000_0011;   //        ?


//read config
reg [23:0]  num_data;               //è¯»å‡ºå­—èŠ‚ä¸ªæ•°
always@(posedge sys_clk or negedge sys_rst_n) begin
    if(!sys_rst_n) begin
        num_data <= 'd0;
    end
    else begin
        if(start) begin
            num_data <= byte_num;
        end
    end
end

reg [7:0] sector_addr;
reg [7:0] page_addr;
reg [7:0] byte_addr;
always@(posedge sys_clk or negedge sys_rst_n) begin
    if(!sys_rst_n) begin
        sector_addr <= 'd0;
        page_addr <= 'd0;
        byte_addr <= 'd0;
    end
    else begin
        if(start) begin
            sector_addr <= start_addr[23:16];
            page_addr <= start_addr[15:8];
            byte_addr <= start_addr[7:0];
        end
    end
end


//reg   define
reg     [4:0]   cnt_clk         ;   //ç³»ç»Ÿæ—¶é’Ÿè®¡æ•°å™?
reg     [2:0]   state           ;   //çŠ¶æ?æœºçŠ¶æ??
reg     [15:0]  cnt_byte        ;   //å­—èŠ‚è®¡æ•°å™?
reg     [1:0]   cnt_sck         ;   //ä¸²è¡Œæ—¶é’Ÿè®¡æ•°å™?
reg     [2:0]   cnt_bit         ;   //æ¯”ç‰¹è®¡æ•°å™?
reg             miso_flag       ;   //misoæå–æ ‡å¿—ä¿¡å·
reg     [7:0]   data            ;   //æ‹¼æ¥æ•°æ®
reg             po_flag_reg     ;   //è¾“å‡ºæ•°æ®æ ‡å¿—ä¿¡å·

//********************************************************************//
//***************************** Main Code ****************************//
//********************************************************************//

//stateï¼šä¸¤æ®µå¼çŠ¶æ?æœºç¬¬ä¸€æ®µï¼ŒçŠ¶æ?è·³è½?
always@(posedge sys_clk or  negedge sys_rst_n)
    if(sys_rst_n == 1'b0)
        state   <=  IDLE;
    else
    case(state)
        IDLE:   if(start == 1'b1)
                    state   <=  READ;
        READ:   if((cnt_byte == num_data + 16'd3) && (cnt_clk == 5'd31))
                    state   <=  IDLE;
    endcase

//mosiï¼šä¸¤æ®µå¼çŠ¶æ?æœºç¬¬äºŒæ®µï¼Œé€»è¾‘è¾“å‡º
always@(posedge sys_clk or  negedge sys_rst_n)
    if(sys_rst_n == 1'b0)
        mosi    <=  1'b0;
    else    if((state == READ) && (cnt_byte>= 16'd4))
        mosi    <=  1'b0;
    else    if((state == READ) && (cnt_byte == 16'd0) && (cnt_sck == 2'd0))
        mosi    <=  READ_INST[7 - cnt_bit];  //è¯»æŒ‡ä»?
    else    if((state == READ) && (cnt_byte == 16'd1) && (cnt_sck == 2'd0))
        mosi    <=  sector_addr[7 - cnt_bit];  //æ‰‡åŒºåœ°å€
    else    if((state == READ) && (cnt_byte == 16'd2) && (cnt_sck == 2'd0))
        mosi    <=  page_addr[7 - cnt_bit];    //é¡µåœ°å?
    else    if((state == READ) && (cnt_byte == 16'd3) && (cnt_sck == 2'd0))
        mosi    <=  byte_addr[7 - cnt_bit];    //å­—èŠ‚åœ°å€

//cnt_clkï¼šç³»ç»Ÿæ—¶é’Ÿè®¡æ•°å™¨ï¼Œç”¨ä»¥è®°å½•å•ä¸ªå­—èŠ?
always@(posedge sys_clk or  negedge sys_rst_n)
    if(sys_rst_n == 1'b0)
        cnt_clk  <=  5'd0;
    else    if(state == READ)
        cnt_clk  <=  cnt_clk + 1'b1;

//cnt_byteï¼šè®°å½•è¾“å‡ºå­—èŠ‚ä¸ªæ•°å’Œç­‰å¾…æ—¶é—´
always@(posedge sys_clk or  negedge sys_rst_n)
    if(sys_rst_n == 1'b0)
        cnt_byte    <=  16'd0;
    else    if((cnt_clk == 5'd31) && (cnt_byte == num_data + 16'd3))
        cnt_byte    <=  16'd0;
    else    if(cnt_clk == 5'd31)
        cnt_byte    <=  cnt_byte + 1'b1;

//cnt_sckï¼šä¸²è¡Œæ—¶é’Ÿè®¡æ•°å™¨ï¼Œç”¨ä»¥ç”Ÿæˆä¸²è¡Œæ—¶é’?
always@(posedge sys_clk or  negedge sys_rst_n)
    if(sys_rst_n == 1'b0)
        cnt_sck <=  2'd0;
    else    if(state == READ)
        cnt_sck <=  cnt_sck + 1'b1;

//cs_nï¼šç‰‡é€‰ä¿¡å?
always@(posedge sys_clk or  negedge sys_rst_n)
    if(sys_rst_n == 1'b0)
        cs_n    <=  1'b1;
    else    if(start == 1'b1)
        cs_n    <=  1'b0;
    else    if((cnt_byte == num_data + 16'd3) && (cnt_clk == 5'd31) && (state == READ))
        cs_n    <=  1'b1;

//sckï¼šè¾“å‡ºä¸²è¡Œæ—¶é’?
always@(posedge sys_clk or  negedge sys_rst_n)
    if(sys_rst_n == 1'b0)
        sck <=  1'b0;
    else    if(cnt_sck == 2'd0)
        sck <=  1'b0;
    else    if(cnt_sck == 2'd2)
        sck <=  1'b1;

//cnt_bitï¼šé«˜ä½ä½å¯¹è°ƒï¼Œæ§åˆ¶mosiè¾“å‡º
always@(posedge sys_clk or  negedge sys_rst_n)
    if(sys_rst_n == 1'b0)
        cnt_bit <=  3'd0;
    else    if(cnt_sck == 2'd2)
        cnt_bit <=  cnt_bit + 1'b1;

//miso_flagï¼šmisoæå–æ ‡å¿—ä¿¡å·
always@(posedge sys_clk or  negedge sys_rst_n)
    if(sys_rst_n == 1'b0)
        miso_flag   <=  1'b0;
    else    if((cnt_byte >= 16'd4) && (cnt_sck == 2'd1))
        miso_flag   <=  1'b1;
    else
        miso_flag   <=  1'b0;

//dataï¼šæ‹¼æ¥æ•°æ?
always@(posedge sys_clk or  negedge sys_rst_n)
    if(sys_rst_n == 1'b0)
        data    <=  8'd0;
    else    if(miso_flag == 1'b1)
        data    <=  {data[6:0],miso};

//po_flag_reg:è¾“å‡ºæ•°æ®æ ‡å¿—ä¿¡å·
always@(posedge sys_clk or  negedge sys_rst_n)
    if(sys_rst_n == 1'b0)
        po_flag_reg <=  1'b0;
    else    if((cnt_bit == 3'd7) && (miso_flag == 1'b1))
        po_flag_reg <=  1'b1;
    else
        po_flag_reg <=  1'b0;

//po_flag:è¾“å‡ºæ•°æ®æ ‡å¿—ä¿¡å·
always@(posedge sys_clk or  negedge sys_rst_n)
    if(sys_rst_n == 1'b0)
        po_flag <=  1'b0;
    else
        po_flag <=  po_flag_reg;

//po_data:è¾“å‡ºæ•°æ®
always@(posedge sys_clk or  negedge sys_rst_n)
    if(sys_rst_n == 1'b0)
        po_data <=  8'd0;
    else    if(po_flag_reg == 1'b1)
        po_data <=  data;
    else
        po_data <=  po_data;

endmodule
